cmake_minimum_required(VERSION 3.0)

project(mgbase)

INCLUDE (CheckCXXCompilerFlag)

option(MGBASE_PROFILER_USE_GPROF "Use Gprof" OFF)
option(MGBASE_PROFILER_USE_GPERFTOOLS "Use Gperftools" OFF)

set(MGBASE_CXX_STANDARD "03" CACHE STRING "C++ standard version")

configure_file(${CMAKE_CURRENT_SOURCE_DIR}/src/config.h.in
    ${CMAKE_CURRENT_BINARY_DIR}/mgbase/config.h)

message(STATUS "Generate mgbase/config.h ...")
message(STATUS "MGBASE_PROFILER_USE_GPROF=${MGBASE_PROFILER_USE_GPROF}")
message(STATUS "MGBASE_PROFILER_USE_GPERFTOOLS=${MGBASE_PROFILER_USE_GPERFTOOLS}")
message(STATUS "MGBASE_CXX_STANDARD=${MGBASE_CXX_STANDARD}")

add_library(mgbase-header INTERFACE)
target_include_directories(mgbase-header
    INTERFACE ./include
    INTERFACE ./external/cppformat
    INTERFACE ./external/cmdline
    INTERFACE ${CMAKE_CURRENT_BINARY_DIR}
)
target_compile_definitions(mgbase-header
    INTERFACE "$<$<CONFIG:Debug>:MGBASE_DEBUG>"
    INTERFACE "$<$<CONFIG:RelWithDebInfo>:MGBASE_DEBUG>"
)

set(error_limit 3)

add_library(mgbase-options INTERFACE)
if ("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Fujitsu")
    target_compile_options(mgbase-options INTERFACE
        -Xg
        -D GTEST_HAS_TR1_TUPLE=0
        --display_error_number
        --diag_suppress 111,161,236,1290,1406
        $<$<CONFIG:Debug>:-g>
        $<$<CONFIG:Release>:-Kfast -O3 -x50>
        $<$<CONFIG:RelWithDebInfo>:-g -Kfast>
    )
    # warning #111-D: statement is unreachable
    #   (cppformat)
    # warning #161-D: unrecognized #pragma
    #   (cppformat, googletest)
    # warning #236-D: controlling expression is constant
    #   (googletest)
    # warning #1290-D: non-POD class type passed through ellipsis
    #   (googletest)
    # warning #1406-D: support for trigraphs is disabled
    #   (googletest)
else()
    target_compile_options(mgbase-options INTERFACE
        #$<$<COMPILE_LANGUAGE:CXX>:-std=c++03> # TODO: COMPILE_LANGUAGE doesn't work on old CMake versions
        -DGTEST_HAS_TR1_TUPLE=0
        -Wno-variadic-macros
    )
    if (("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Clang") OR ("${CMAKE_CXX_COMPILER_ID}" STREQUAL "AppleClang"))
        target_compile_options(mgbase-options INTERFACE
            # C++ Standard
            -std=c++${MGBASE_CXX_STANDARD}
            
            # Enable -Weverything
            -Weverything
            
            # Disable some warnings of -Weverything
            -Wno-global-constructors
            -Wno-exit-time-destructors
            -Wno-padded # TODO: alignment issues
            -Wno-disabled-macro-expansion # Problem of stdbool.h
            -Wno-used-but-marked-unused # e.g. mgbase::try_to_lock
            -Wno-weak-vtables # couldn't find a correct way to suppress this warning
            $<$<VERSION_GREATER:${MGBASE_CXX_STANDARD},03>:-Wno-c++98-compat -Wno-c++98-compat-pedantic>
            
            # Other options
            -Wno-c++11-long-long # TODO
            -ferror-limit=${error_limit}
        )
    elseif ("${CMAKE_CXX_COMPILER_ID}" STREQUAL "GNU")
        CHECK_CXX_COMPILER_FLAG("-Wc++14-compat" MGBASE_GCC_SUPPORTS_WCXX14_COMPAT)
        CHECK_CXX_COMPILER_FLAG("-Wconditionally-supported" MGBASE_GCC_SUPPORTS_WCONDITINALLY_SUPPORTED)
        CHECK_CXX_COMPILER_FLAG("-Wdate-time" MGBASE_GCC_SUPPORTS_WDATE_TIME)
        CHECK_CXX_COMPILER_FLAG("-Wdelete-non-virtual-dtor" MGBASE_GCC_SUPPORTS_WDELETE_NON_VIRTUAL_DTOR)
        CHECK_CXX_COMPILER_FLAG("-Wdouble-promotion" MGBASE_GCC_SUPPORTS_WDOUBLE_PROMOTION)
        CHECK_CXX_COMPILER_FLAG("-Wformat-signedness" MGBASE_GCC_SUPPORTS_WFORMAT_SIGNEDNESS)
        CHECK_CXX_COMPILER_FLAG("-Wnoexcept" MGBASE_GCC_SUPPORTS_WNOEXCEPT)
        CHECK_CXX_COMPILER_FLAG("-Wsuggest-attribute=format" MGBASE_GCC_SUPPORTS_WSUGGEST_ATTRIBUTE)
        CHECK_CXX_COMPILER_FLAG("-Wsuggest-override" MGBASE_GCC_SUPPORTS_WSUGGEST_OVERRIDE)
        CHECK_CXX_COMPILER_FLAG("-Wuseless-cast" MGBASE_GCC_SUPPORTS_WUSELESS_CAST)
        CHECK_CXX_COMPILER_FLAG("-Wzero-as-null-pointer-constant" MGBASE_GCC_SUPPORTS_WZERO_AS_NULL_POINTER_CONSTANT)
        
        target_compile_options(mgbase-options INTERFACE
            # C++ Standard
            -std=c++${MGBASE_CXX_STANDARD}
            
            # Meta warnings
            -Wall
            -Wextra
            -pedantic
            
            # Disable warnings
            -Wno-long-long # TODO
            
            # Generated by this command:
            #   $ gcc -Q --help=warning -Wall -pedantic -Wextra | grep disabled | cut -f 1
            -Wabi
            #-Wabi-tag # Seems useless
            -Wc++0x-compat
            $<$<BOOL:${MGBASE_GCC_SUPPORTS_WCXX14_COMPAT}>:-Wc++14-compat>
            -Wcast-qual
            #-Wchkp # Undocumented warning option
            $<$<BOOL:${MGBASE_GCC_SUPPORTS_WCONDITINALLY_SUPPORTED}>:-Wconditionally-supported>
            -Wconversion
            -Wctor-dtor-privacy
            $<$<BOOL:${MGBASE_GCC_SUPPORTS_WDATE_TIME}>:-Wdate-time>
            $<$<BOOL:${MGBASE_GCC_SUPPORTS_WDELETE_NON_VIRTUAL_DTOR}>:-Wdelete-non-virtual-dtor>
            $<$<BOOL:${MGBASE_GCC_SUPPORTS_WDOUBLE_PROMOTION}>:-Wdouble-promotion>
            #-Weffc++ # TODO: Too strict?
            -Wfloat-equal
            -Wformat-nonliteral
            -Wformat-security
            $<$<BOOL:${MGBASE_GCC_SUPPORTS_WFORMAT_SIGNEDNESS}>:-Wformat-signedness>
            -Wformat-y2k
            -Winit-self
            -Winvalid-pch
            -Wlogical-op
            -Wmissing-declarations
            -Wmissing-include-dirs
            -Wmultichar
            $<$<BOOL:${MGBASE_GCC_SUPPORTS_WNOEXCEPT}>:-Wnoexcept>
            -Wnon-virtual-dtor
            -Wold-style-cast
            -Woverloaded-virtual
            -Wredundant-decls
            -Wreorder
            -Wsign-promo
            -Wstrict-null-sentinel
            $<$<BOOL:${MGBASE_GCC_SUPPORTS_WSUGGEST_ATTRIBUTE}>:-Wsuggest-attribute=format>
            $<$<BOOL:${MGBASE_GCC_SUPPORTS_WSUGGEST_OVERRIDE}>:-Wsuggest-override>
            -Wswitch-default
            -Wswitch-enum
            -Wsynth
            #-Wsystem-headers # Disable warnings from system headers
            -Wundef
            -Wunused-macros
            $<$<BOOL:${MGBASE_GCC_SUPPORTS_WUSELESS_CAST}>:-Wuseless-cast>
            -Wwrite-strings
            $<$<BOOL:${MGBASE_GCC_SUPPORTS_WZERO_AS_NULL_POINTER_CONSTANT}>:-Wzero-as-null-pointer-constant>
            
            # Other warnings
            -Wstrict-overflow=5
        )
    elseif ("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Intel")
        target_compile_options(mgbase-options INTERFACE
            # C++ Standard
            -std=c++${MGBASE_CXX_STANDARD}
            
            # Enable -Wall
            -Wall
            
            # Disable some warnings
            -Wno-long-long
            -Wno-shadow # ICC cannot disable this warning in template classes/functions
        )
    endif()
    
    if(MGBASE_PROFILER_USE_GPROF)
        target_compile_options(mgbase-options INTERFACE
            $<$<CXX_COMPILER_ID:GNU>:-pg>
        )
        target_link_libraries(mgbase-options INTERFACE "-pg")
    endif()
endif()


add_library(mgbase
    ./external/cppformat/cppformat/format.cc
)
target_link_libraries(mgbase mgbase-header)

if (MGBASE_PROFILER_USE_GPERFTOOLS)
    target_link_libraries(mgbase profiler)
endif()

if ("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Fujitsu")
    target_compile_options(mgbase
        PRIVATE
        -Xg
        -include ${CMAKE_CURRENT_SOURCE_DIR}/include/mgbase/external/cppformat.hpp
    )
endif()

add_library(googletest
    ./external/googletest/googletest/src/gtest-all.cc
)
target_compile_options(googletest
    PRIVATE
    -w # Suppress ALL warnings
)
target_include_directories(googletest
    PUBLIC ./external/googletest/googletest
    PUBLIC ./external/googletest/googletest/include
)
target_link_libraries(googletest
    PRIVATE mgbase-options
)
if ("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Fujitsu")
    target_sources(googletest
        PRIVATE ./src/workaround/wcscasecmp.cpp
    )
    target_compile_options(googletest
        PRIVATE
        -Xg
        -include ${CMAKE_CURRENT_SOURCE_DIR}/src/workaround/wcscasecmp.h
        -D GTEST_HAS_TR1_TUPLE=0
    )
endif()

add_executable(mgbase-unittest
    unittest/unittest.cpp
    unittest/deferred.cpp
    unittest/thread.cpp
    unittest/callback_function.cpp
    unittest/bound_function.cpp
    unittest/spinlock.cpp
    unittest/mpsc_circular_buffer.cpp
    unittest/condition_variable.cpp
    unittest/optional.cpp
    unittest/move.cpp
)

target_link_libraries(mgbase-unittest mgbase mgbase-options googletest)


